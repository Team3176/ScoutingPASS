// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "33.0.0"
        minSdkVersion = 24
        compileSdkVersion = 33
        targetSdkVersion = 33
        kotlinVersion = '1.8.22'
        composeCompilerVersion = '1.4.8'
        ndkVersion = "26.1.10909125"
        frescoVersion = "2.5.0"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle')
        classpath('com.facebook.react:react-native-gradle-plugin')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    }
}

apply plugin: "com.facebook.react.rootproject"

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url(new File(['node', '--print', "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim(), '../android'))
        }
        maven {
            // Android JSC is installed from npm
            url(new File(['node', '--print', "require.resolve('jsc-android/package.json', { paths: [require.resolve('react-native/package.json')] })"].execute(null, rootDir).text.trim(), '../dist'))
        }

        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
    }
}

// Force all projects to use the same Kotlin version
subprojects { project ->
    project.plugins.withType(com.android.build.gradle.BasePlugin).configureEach {
        android {
            buildToolsVersion rootProject.ext.buildToolsVersion
            compileSdkVersion rootProject.ext.compileSdkVersion

            defaultConfig {
                minSdkVersion rootProject.ext.minSdkVersion
                targetSdkVersion rootProject.ext.targetSdkVersion
            }

            kotlinOptions {
                jvmTarget = "1.8"
            }

            if (project.hasProperty('composeOptions')) {
                composeOptions {
                    kotlinCompilerExtensionVersion rootProject.ext.composeCompilerVersion
                }
            }
        }
    }
}
